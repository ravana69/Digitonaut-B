<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/bacterium/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 07:40:55 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Bacterium</title>

<link type="text/css" rel="stylesheet" href="main.css">
</head>
<body>

<script id="vs" type="not-js">
void main()	{
  gl_Position = vec4( position, 1.0 );
}
</script>
<script id="fs" type="not-js">
precision mediump float;

// -------------------------------------------- code from https://glslsandbox.com/ ------------------

//---------------------------------------------------------
// Shader:   Bacterium.glsl                      12/2014
//           Created by Vinicius Graciano Santos - vgs 
// original: https://www.shadertoy.com/view/MdBSDt
// tags:     raymarch, antialiasing, antialias, bacteria
//---------------------------------------------------------

#ifdef GL_ES
  precision highp float;
#endif

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

//---------------------------------------------------------
float tri(in float x){return abs(fract(x)-.5);}
vec3 tri3(in vec3 p){return vec3( tri(p.z+tri(p.y*1.))
                                , tri(p.z+tri(p.x*1.))
                                , tri(p.y+tri(p.x*1.)));}
float triNoise3d(in vec3 p)
{
    float z=1.4;
    float rz = 0.;
    vec3 bp = p;
    for (float i=0.; i<= 2.; i++ )
    {
        vec3 dg = tri3(bp);
        p += (dg);
        bp *= 2.;
	z *= 1.5;
	p *= 1.3;
        rz += (tri(p.z+tri(p.x+tri(p.y))))/z;
        bp += 0.14;
    }
    return rz;
}
//---------------------------------------------------------
// This shader needs some serious work on collision avoidance :D

#define STEPS 66
#define EPS 0.002
#define FAR 18.0
#define PI 3.14159265359

float smin(float a, float b, float k) {
    float h = clamp(.5+.5*(b-a)/k, 0.0, 1.0 );
    return mix(b,a,h)-k*h*(1.-h);
}

vec2 rep(vec2 p) {
    float a = atan(p.y, p.x);
    a = mod(a, 2.0*PI/18.) - PI/18.;
    return length(p)*vec2(cos(a), sin(a));
}

float spikedBall(vec3 p) {
    p = mod(p, 8.0) - 4.0;
    float d = length(p) - 1.2;
    p.xz = rep(p.xz); p.xy = rep(p.xy); 
    return smin(d, length(p.yz)-.1+abs(.15*(p.x-1.0)), 0.1);
}

float capsules(vec3 p) {
    vec3 q = floor(p/4.0);
    p = mod(p, 4.0) - 2.0;
    p.yz = p.yz*cos(time + q.z) + vec2(-p.z, p.y)*sin(time + q.z);
    p.xy = p.xy*cos(time + q.x) + vec2(-p.y, p.x)*sin(time + q.x);
    p.zx = p.zx*cos(time + q.y) + vec2(-p.x, p.z)*sin(time + q.y);
    
    float angle = .3*cos(time)*p.x;
    p.xy = cos(angle)*p.xy + sin(angle)*vec2(-p.y, p.x); p.x += 1.0; 
    float k = clamp(2.0*p.x/4.0, 0.0, 1.0); p.x -= 2.*k;
    return length(p) - .5;
}

float map(vec3 p) {   
   return min(spikedBall(p), capsules(p));
}

vec3 normal(vec3 p) {
    vec2 q = vec2(0.0, EPS);
    return normalize(vec3(map(p + q.yxx) - map(p - q.yxx),
                          map(p + q.xyx) - map(p - q.xyx),
                          map(p + q.xxy) - map(p - q.xxy)));
}

float cubeMap(vec3 p, vec3 n) {
    float a = triNoise3d(p*2.0);
    n = abs(n);
    return (a*n.x + 2.0*a*n.y + a*n.z) / (n.x+n.y+n.z);   
}

vec3 bumpMap(vec3 p, vec3 n, float c) {
    vec2 q = vec2(0.0, .5);
	vec3 grad = -1.0*(vec3(cubeMap(p+q.yxx, n), cubeMap(p+q.xyx, n), cubeMap(p+q.xxy, n))-c)/q.y;
    vec3 t = grad - n*dot(grad, n);
    return normalize(n - t);
}

vec3 shade(vec3 ro, vec3 rd, float t) {
    vec3 p = ro + t*rd, n = normal(p);
   
    vec3 green = pow(vec3(93,202,49)/255., vec3(2.2));
    vec3 yellow = pow(vec3(255,204,0)/255., vec3(2.2));
    
    float k = cubeMap(.5*p, n);
    n = bumpMap(.5*p, n, k);
    
    vec3 col = mix(green, yellow, k)*(1.0-dot(-rd,n));
    if (spikedBall(p) < capsules(p)) {
    	p = mod(p, 8.0) - 4.0;
        col *= 1.0/(1.0 + .5*dot(p, p));
    }
        
    return col*exp(-.008*t*t);
}

mat3 lookat(vec3 p, vec3 t) {
    vec3 z = normalize(p - t);
    vec3 x = cross(z, vec3(0.0, 1.0, 0.0));
    return mat3(x, cross(x, z), z);
}

void main() 
{
    vec2 uv = (-resolution.xy + 2.0*gl_FragCoord.xy) / resolution.y;
    uv *= 1.0 + .1*dot(uv,uv);
    
    vec3 ro = vec3(time, time, cos(time));
    vec3 rd = normalize(lookat(ro, ro+vec3(cos(.1*time), sin(.1*time), 1.0))*vec3(uv, -1.0)); // direção do raio.
    
    // based on eiffie's antialiasing method (https://www.shadertoy.com/view/XsSXDt)
    vec3 col = vec3(0.0);
    vec4 stack = vec4(-1.0); bool grab = true;
    float t = 0.0, d = EPS, od = d, pix = 4.0/resolution.x, w = 1.8, s = 0.0;
    for (int i = 0; i < STEPS; ++i)
    {
        d = map(ro + t*rd);
        if (w > 1.0 && (od + d < s)) 
        {
            s -= w*s; w = 1.0;
        } 
		else 
		{
            s = d * w;   
            if (d <= od) grab = true;
            else if (grab && stack.w < 0. && od < pix*(t-od)) 
		    {
		        stack.w = t-od; 
		        stack = stack.wxyz; 
		        grab = false;
            }
            if (d < EPS || t > FAR) break;
        }
        od = d; t += s; 
    }
    col = d < EPS ? shade(ro, rd, t) : col;
    
    for (int i = 0; i < 4; ++i) 
	{
        if (stack[i] < 0.0) break;
        d = map(ro + stack[i]*rd);
        col = mix(shade(ro, rd, stack[i]), col, clamp(d/(pix*stack[i]), 0.0, 1.0));
    }
    col = smoothstep(0., .7, col);
    col = pow(col, vec3(1.0/2.2));
    
    gl_FragColor = vec4(col,1.0);
}

// -------------------------------------------------------------------------------------------------------------------

</script>
<script src="three.min.js"></script>
<script src="main.js"></script>

</body>

<!-- Mirrored from digitonaut.com/art/bacterium/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 07:41:00 GMT -->
</html>
